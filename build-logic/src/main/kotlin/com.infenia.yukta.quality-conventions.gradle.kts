plugins {
    id("com.diffplug.spotless")
    checkstyle
    pmd
    id("com.github.spotbugs")
}

val libs = extensions.getByType<VersionCatalogsExtension>().named("libs")

spotless {
    java {
        licenseHeaderFile(rootProject.file("config/license/header.txt"))
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat().reflowLongStrings()
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
        targetExclude("bin/**", "build/**", "out/**", "**/.gradle/**")
    }

    kotlinGradle {
        target("*.gradle.kts", "build-logic/**/*.gradle.kts")
        licenseHeaderFile(rootProject.file("config/license/header.txt"), "(plugins|id|import|apply)")
    }

    format("xml") {
        target("**/*.xml")
        targetExclude("**/build/**", "**/bin/**", "**/out/**", "**/.gradle/**", "**/node_modules/**")
        licenseHeaderFile(rootProject.file("config/license/header-xml.txt"), "(<[^!?])")
    }
}

checkstyle {
    toolVersion = libs.findVersion("checkstyle").get().requiredVersion
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    isIgnoreFailures = false
    isShowViolations = true
    maxWarnings = 0
}

pmd {
    toolVersion = libs.findVersion("pmd").get().requiredVersion
    ruleSets = listOf(rootProject.file("config/pmd/ruleset.xml").absolutePath)
    isIgnoreFailures = false
    isConsoleOutput = true
}

spotbugs {
    excludeFilter.set(rootProject.file("config/spotbugs/exclude.xml"))
}

tasks.register<Exec>("semgrep") {
    description = "Run Semgrep static analysis"
    group = "verification"

    val sourceDir = project.file("src/main/java")
    val configFile = rootProject.file("config/semgrep/.semgrep.yml")
    val reportDir = project.layout.buildDirectory.dir("reports/semgrep").get()
    val reportFile = reportDir.file("semgrep-report.sarif").asFile

    doFirst {
        if (!sourceDir.exists()) {
            logger.warn("Source directory does not exist: $sourceDir, skipping Semgrep scan")
            enabled = false
            return@doFirst
        }
        reportDir.asFile.mkdirs()
    }

    commandLine("semgrep", "scan",
        "--config=${configFile.absolutePath}",
        "--output=${reportFile.absolutePath}",
        "--json",
        "--quiet",
        sourceDir.absolutePath
    )

    isIgnoreExitValue = true

    doLast {
        // Exit code 1 means findings were reported, which is not a failure
        // Exit code 0 means no findings
        if (executionResult.get().exitValue > 1) {
            throw GradleException("Semgrep scan failed with exit code ${executionResult.get().exitValue}")
        }
    }
}

tasks.withType<Checkstyle>().configureEach {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

// Disable quality tasks for everything except main
tasks.configureEach {
    val task = this
    if (project.name == "ui" && task::class.java.name.contains("SpotBugs")) {
        task.enabled = false
    }
    if (project.name == "cli" && (task is Pmd || task::class.java.name.contains("SpotBugs"))) {
        task.enabled = false
    }
    if ((task.name.contains("Aot") || task.name.contains("Test")) &&
        (task is Checkstyle || task is Pmd || task::class.java.name.contains("SpotBugs"))) {
        task.enabled = false
    }
    if ((task.name.contains("Aot") || task.name.contains("Test")) && task.name == "semgrep") {
        task.enabled = false
    }
}
