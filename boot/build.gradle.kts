/*
 * Copyright 2026 Infenia Private Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id("com.infenia.yukta.spring-conventions")
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.graalvm.buildtools.native)
}

dependencies {
    implementation(project(":core"))
    implementation(project(":web"))
    implementation(project(":mcp"))
    implementation(project(":ui"))
    implementation(project(":plugins:processors:internal:internal-core"))
    implementation(project(":plugins:triggers:api-trigger"))
    implementation(project(":plugins:triggers:constant-source"))
    implementation(project(":plugins:terminals:console-terminal"))
    implementation(project(":plugins:processors:process-executor"))

    implementation(libs.spring.boot.starter.webflux)
    implementation(libs.spring.boot.starter.validation)
    implementation(libs.spring.boot.starter.actuator)

    developmentOnly(libs.spring.boot.devtools)
    developmentOnly(libs.spring.boot.docker.compose)

    annotationProcessor(libs.spring.boot.configuration.processor)

    testImplementation(libs.spring.boot.starter.webflux.test)
    testImplementation(libs.spring.boot.starter.validation.test)
    testImplementation(libs.spring.boot.starter.actuator.test)
}

tasks.named<org.springframework.boot.gradle.tasks.run.BootRun>("bootRun") {
    mainClass.set("com.infenia.yukta.YuktaApplication")
    args("--spring.profiles.active=dev")
    standardInput = System.`in`
    // Ensure the application always starts even if no files changed
    outputs.upToDateWhen { false }
}

coverageConfig {
    exceptions.put("com.infenia.yukta.YuktaApplication", mapOf(
        "LINE" to 0.5,
        "BRANCH" to 0.1,
        "CLASS" to 0.8,
        "INSTRUCTION" to 0.3,
        "METHOD" to 0.7
    ))
}

graalvmNative {
    binaries {
        named("main") {
            imageName.set("yukta")
            mainClass.set("com.infenia.yukta.YuktaApplication")
            buildArgs.add("--no-fallback")

            // Aggressive size-reduction flags
            buildArgs.add("-H:+RemoveUnusedSymbols")  // Strip unused symbols
            buildArgs.add("-H:+StripDebugInfo")        // Remove debug information
            buildArgs.add("--gc=G1")                    // Use G1 garbage collector
            buildArgs.add("-R:MaxHeapSize=512m")        // Set runtime max heap size
            buildArgs.add("-J-Xmx4g")                  // JVM max heap (build time)

            // Enable AOT compilation for native image
            buildArgs.add("-Dspring.aot.enabled=true")
        }
    }
}
